/*
 * Copyright 2014-2017 ISP RAS (http://www.ispras.ru)
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

/*
 * Description:
 *
 * Example of a specification of a trivial microprocessor ISA.
 * Provided to demonstrate various features of nML.
 */

////////////////////////////////////////////////////////////////////////////////
// Constants

let   MEM_SIZE = 2 ** 16 // Memory size, 2 to the power of 8
let REG_NUMBER = 16     // Number of general-purpose registers

////////////////////////////////////////////////////////////////////////////////
// Types

type Addr_t    = card(16) // 8-bit unsigned integer
type RegAddr_t = card(4) // 4-bit unsigned integer
type Word_t    = int(16)  // 8-bit signed integer

////////////////////////////////////////////////////////////////////////////////
// Registers and Memory

// General-purpose registers.
reg GPR[REG_NUMBER, Word_t] // Format: <name>[<number>, <type>]

// A register for storing the program counter.
reg PC[Addr_t] // This format means that there is 1 register of type Addr_t

// A memory line
mem M[MEM_SIZE, Word_t]

////////////////////////////////////////////////////////////////////////////////
// Temporary Variables
//
// They are not preserved across instruction calls and serve to exchange data
// between operations that make up an instruction.

var SRC1[Word_t]
var SRC2[Word_t]
var DEST[Word_t]

////////////////////////////////////////////////////////////////////////////////
// Addressing Modes

// An addressing mode for an immediate value.
mode IMM(i: int(6)) = sign_extend(Word_t, i) // Value expression
  syntax = format("[%d]", i) // Textual format
  image  = format("%6s", i)  // Binary format

// An addressing mode for a register access.
mode REG(i: RegAddr_t) = GPR[i]
  syntax = format("x%d", i)
  image  = format("00%4s", i)

// An addressing mode for a memory access.
mode MEM(i: card(6)) = M[i]
  syntax = format("(%d)", i)
  image  = format("%6s", i)

// Addressing modes are united into groups.
mode OPRNDL = MEM | REG
mode OPRNDR = OPRNDL | IMM

////////////////////////////////////////////////////////////////////////////////
// Arithmetic and Logic Instructions

op ADD()
  syntax = "add"
  image  = "00"
  action = {
    DEST = SRC1 + SRC2;
    // Function 'trace' prints text messages to the simulator log
    trace("%d + %d = %d", SRC1, SRC2, DEST);
  }

op SUB()
  syntax = "sub"
  image  = "01"
  action = { DEST = SRC1 - SRC2; }

op MOV()
  syntax = "mov"
  image  = "10"
  action = { DEST = SRC1; }

// A common alias for ADD, SUB and MOV
op ALU = ADD | SUB
op ALU_op1 = MOV

// A common specification of all ALU instructions that describes their common
// format, performs all common actions and delegates unique responsibilities
// to specific operations (or subinstructions).

op alu_instr(command: ALU, dst: OPRNDL, op1: OPRNDR, op2: OPRNDR)
  syntax = format("%s %s %s %s", command.syntax, dst.syntax, op1.syntax, op2.syntax)
  image  = format("%s%s00%s0001000100010001", command.image, op1.image, op2.image)
  action = {
    SRC1 = op1;
    SRC2 = op2;
    command.action;
    op1 = DEST;
    PC = PC + 4;
  }


op op1_instr (command: ALU_op1, dst: OPRNDL, op1: OPRNDR)
  syntax = format("%s %s %s", command.syntax, dst.syntax, op1.syntax)
  image  = format("%s%s00%s0010001000100010", command.image, dst.image, op1.image)
  action = {
    SRC1 = op1;
    command.action;
    dst = DEST;
    PC = PC + 4;
  }


////////////////////////////////////////////////////////////////////////////////
// Control Transfer Instructions

// Transfers control to the specified address.
//
// IMPORTANT NOTE: The target address can be specified via a label. In order to
// use the label name rather than an address constant in the generated test
// program, special label-based addressing modes must be described in
// the specifications.

label mode J_LABEL(value: Addr_t) = value
  syntax = ""
  image  = format("%s", value)

mode J_IMM(value: Addr_t) = value
  syntax = format("0x%X", value)
  image  = format("%s", value)

mode J_ADDR = J_LABEL | J_IMM

op J (target : J_ADDR)
  syntax = format("j %s", target.syntax)
  image  = format("01%s0110011", target.image)
  action = { PC = target; }

// Transfers control to the specified address if source equals 0.
op JZ (source: OPRNDL, target: J_ADDR)
  syntax = format("jz %s", target.syntax)
  image  = format("11%s%s01000100", source.image, target.image)
  action = {
    if source == 0 then
      PC = target;
    else
      PC = PC + 4;
    endif;
  }

op branch_instr = J | JZ

////////////////////////////////////////////////////////////////////////////////
// Entry Point
//
// By nML conventions, the "instruction" operation is the root of the tree
// describing a microprocessor ISA.

op instruction = alu_instr
               | op1_instr
               | branch_instr
